buildscript {
  apply from: rootProject.file('../gradle/dependencies.gradle')

  repositories {
    mavenLocal()
    google()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // For Spoon snapshot, until 2.0.0 is released
    gradlePluginPortal()
  }

  dependencies {
    classpath deps.android.tools.build.gradle
    classpath deps.kotlin.gradle.plugin
    classpath 'com.jaredsburrows:gradle-spoon-plugin:latest.integration'
    classpath 'com.gradle:build-scan-plugin:2.4.2'
  }
}

repositories {
  google()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // For Spoon snapshot, until 2.0.0 is released
  gradlePluginPortal()
}

apply plugin: 'com.gradle.build-scan'
apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.jaredsburrows.spoon'

android {
  compileSdkVersion rootProject.buildConfig.compileSdk

  defaultConfig {
    applicationId 'com.jaredsburrows.spoon.example'
    minSdkVersion rootProject.buildConfig.minSdk
    targetSdkVersion rootProject.buildConfig.targetSdk
    versionCode 1
    versionName '1.0'

    testApplicationId 'com.jaredsburrows.spoon.example.test'
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
  }
}

spoon {
  debug = true

  baseOutputDir = file("$buildDir/custom-report-dir")

  if (project.hasProperty('spoonClassName')) {
    className = project.spoonClassName

    if (project.hasProperty('spoonMethodName')) {
      methodName = project.spoonMethodName
    }
  }

  instrumentationArgs = ['disableAnalytics:true']

  adbTimeout = 30

  codeCoverage = true

  grantAllPermissions = true
}

configurations.all {
  resolutionStrategy {
    eachDependency { details ->
      if (details.requested.group == 'org.jetbrains.kotlin') {
        details.useVersion versions.kotlin
      }
      if (details.requested.group == 'org.jacoco') {
        details.useVersion versions.jacoco
      }
    }
  }
}

dependencies {
  implementation deps.kotlin.stdlib.jdk

  androidTestImplementation deps.truth
  androidTestImplementation deps.junit
  androidTestImplementation deps.spoon.client
  androidTestImplementation deps.androidx.test.core
  androidTestImplementation deps.androidx.test.junit
  androidTestImplementation deps.androidx.test.runner
  androidTestImplementation deps.androidx.espresso.core
  androidTestImplementation deps.androidx.espresso.intents
  androidTestImplementation deps.androidx.espresso.contrib

  androidTestUtil deps.androidx.test.orchestrator
}

apply from: rootProject.file('../gradle/scan.gradle')
apply from: rootProject.file('../gradle/compile.gradle')

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
  kotlinOptions {
    allWarningsAsErrors = true
  }
}

tasks.register("checkReports") {
  doLast {
    assert project.tasks.spoonDebugAndroidTest != null
    assert project.file("$buildDir/custom-report-dir").exists()
    assert project.file("$buildDir/custom-report-dir/debug").exists()
    assert project.file("$buildDir/custom-report-dir/debug/coverage").exists()
  }
}
