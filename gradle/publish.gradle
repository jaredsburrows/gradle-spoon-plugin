def isReleaseBuild() {
  return !VERSION_NAME.contains('SNAPSHOT')
}

def getRepositoryUsername() {
  return project.properties['SONATYPE_USERNAME'] ?: System.getenv('SONATYPE_USERNAME')
}

def getRepositoryPassword() {
  return project.properties['SONATYPE_PASSWORD'] ?: System.getenv('SONATYPE_PASSWORD')
}

def getReleaseRepositoryUrl() {
  if (isReleaseBuild()) {
    return 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
  } else {
    return 'https://oss.sonatype.org/content/repositories/snapshots/'
  }
}

task docsJar(type: Jar, dependsOn: dokka) {
  group = 'Publications'
  description = 'Create jar of documentation.'
  archiveClassifier = 'docs'
  from dokka.outputDirectory
}

task sourcesJar(type: Jar, dependsOn: classes) {
  group = 'Publications'
  description = 'Create jar of sources.'
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

task testsJar(type: Jar) {
  group = 'Publications'
  description = 'Create jar of tests.'
  archiveClassifier = 'tests'
  from sourceSets.test.output
}

task reportsZip(type: Zip, dependsOn: check) {
  group = 'Publications'
  description = 'Create a zip of all reports.'
  archiveClassifier = 'reports'
  from reporting.baseDir
}

publishing {
  repositories {
    mavenLocal()

    maven {
      credentials {
        username getRepositoryUsername()
        password getRepositoryPassword()
      }

      url getReleaseRepositoryUrl()
    }
  }

  publications {
    maven(MavenPublication) {
      from components.java // includes main 'artifact jar'
      artifact docsJar
      artifact sourcesJar
      artifact testsJar
      artifact reportsZip

      groupId GROUP
      artifactId POM_ARTIFACT_ID
      version VERSION_NAME

      pom {
        name = POM_ARTIFACT_ID
        packaging = POM_PACKAGING
        description = POM_DESCRIPTION
        url = POM_URL
        inceptionYear = POM_INCEPTION_YEAR

        licenses {
          license {
            name = POM_LICENSE_NAME
            url = POM_LICENSE_URL
            distribution = POM_LICENSE_DIST
          }
        }

        developers {
          developer {
            id = POM_DEVELOPER_ID
            name = POM_DEVELOPER_NAME
            email = POM_DEVELOPER_EMAIL
          }
        }

        scm {
          url = POM_SCM_URL
          connection = POM_SCM_CONNECTION
          developerConnection = POM_SCM_DEV_CONNECTION
        }

        issueManagement {
          system = POM_ISSUE_SYSTEM
          url = POM_ISSUE_URL
        }
      }
    }
  }
}

if (isReleaseBuild()) {
  signing {
    required {
      isReleaseBuild()
    }
    sign publishing.publications.maven
  }
}

// Publishes to Gradle plugins repository
gradlePlugin {
  plugins {
    spoonPlugin {
      id = PLUGIN_NAME
      implementationClass = PLUGIN_NAME_CLASS
    }
  }
}
pluginBundle {
  website = POM_URL
  vcsUrl = POM_URL
  description = POM_DESCRIPTION

  plugins {
    spoonPlugin {
      displayName = POM_NAME
    }
  }
}
publishPlugins.dependsOn jar, docsJar, sourcesJar, testsJar, reportsZip
publishPlugins.dependsOn 'generatePomFileForMavenPublication'

// Publish to both Bintray and Gradle repositories
task release(dependsOn: [publish, publishPlugins]) {
  group = 'Publishing'
  description = 'Publish to Maven Central and Gradle Plugins Repositories.'
}
