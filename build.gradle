plugins {
  id "com.gradle.build-scan" version "1.12.1"
  id "com.jfrog.artifactory" version "4.4.18"
  id "com.jfrog.bintray" version "1.8.0"
  id "com.github.ben-manes.versions" version "0.20.0"
  id "com.gradle.plugin-publish" version "0.9.10"
  id "org.jetbrains.kotlin.jvm" version "1.2.30"
  id "org.jetbrains.dokka" version "0.9.16"
  id "java-gradle-plugin"
  id "groovy"
  id "maven-publish"
}

repositories {
  jcenter()
  google()
  maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // For Spoon snapshot, until 2.0.0 is released
}

apply from: "gradle/dependencies.gradle"

group = GROUP
version = VERSION_NAME
description = POM_DESCRIPTION

sourceCompatibility = ext.javaVersion
targetCompatibility = ext.javaVersion

jar {
  manifest {
    attributes(
      "Implementation-Title": POM_NAME,
      "Implementation-Version": version,
      "Built-By": System.getProperty("user.name"),
      "Built-Date": new Date(),
      "Built-JDK": System.getProperty("java.version"),
      "Built-Gradle": gradle.gradleVersion)
  }
}

configurations.all {
  resolutionStrategy.cacheDynamicVersionsFor 0, "seconds"
}

dependencies {
  implementation deps.kotlinStdlib
  implementation deps.kotlinReflect
  implementation deps.gradle
  implementation deps.spoonRunner
  implementation deps.ddmlib

  testImplementation localGroovy()
  testImplementation deps.spockCore, { exclude module: "groovy-all" } // Use localGroovy()
}

apply from: "gradle/scan.gradle"
apply from: "gradle/compile.gradle"
apply from: "gradle/plugin.gradle"
apply from: "gradle/publish.gradle"

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
  kotlinOptions {
    jvmTarget = rootProject.ext.javaVersion
    allWarningsAsErrors = true
  }
}

tasks.withType(org.jetbrains.dokka.gradle.DokkaTask) {
  moduleName = "${project.name} ${project.version}"
}
