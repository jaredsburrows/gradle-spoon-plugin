import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.9.21'
  id 'org.jetbrains.dokka' version '1.9.10' apply false
  id 'org.jlleitschuh.gradle.ktlint' version '11.6.1' apply false
  id 'com.vanniktech.maven.publish' version '0.25.3' apply false
  id 'com.gradle.plugin-publish' version '0.21.0' apply false
  id 'com.github.ben-manes.versions' version '0.49.0'
  id 'java-gradle-plugin'
  id 'java-library'
  id 'groovy'
}

apply from: 'gradle/dependencies.gradle'

configurations.configureEach {
  resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'

  resolutionStrategy {
    eachDependency { details ->
      if (details.requested.group == 'org.jetbrains.kotlin') {
        details.useVersion versions.kotlin
      }
      if (details.requested.group == 'org.jacoco') {
        details.useVersion versions.jacoco
      }
    }
  }
}

subprojects {
  repositories {
    gradlePluginPortal()
    google()
    mavenCentral()
    // For Spoon snapshot, until 2.0.0 is released
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  }

  tasks.withType(Jar).configureEach {
    def dateFile = new File(buildDir, 'jar-manifest-date.txt')
    if (!dateFile.exists()) {
      def date = DateTimeFormatter.ofPattern('EEE MMM dd HH:mm:ss zzz yyyy').
        format(ZonedDateTime.now())
      dateFile.parentFile.mkdirs()
      dateFile.text = date.trim()
    }

    manifest {
      attributes(
        'Created-By': POM_DEVELOPER_NAME,
        'Implementation-Title': POM_NAME,
        'Implementation-Version': VERSION_NAME,
        'Implementation-Vendor': POM_DEVELOPER_NAME,
        'Built-By': System.getProperty('user.name'),
        'Built-Date': dateFile.text.trim(),
        'Built-JDK': System.getProperty('java.version'),
        'Built-Gradle': gradle.gradleVersion)
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = JavaVersion.VERSION_1_8
    }
  }

  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    configure(options) {
      compilerArgs << '-Xlint:all'
      compilerArgs << '-Xlint:-options'
      encoding = 'utf-8'
      fork = true
    }
  }

  tasks.withType(GroovyCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    configure(options) {
      compilerArgs << '-Xlint:all'
      compilerArgs << '-Xlint:-options'
      encoding = 'utf-8'
      fork = true
    }
  }

  tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
      exceptionFormat 'full'
      showCauses true
      showExceptions true
      showStackTraces true
      events 'failed', 'skipped'
    }

    def maxWorkerCount = gradle.startParameter.maxWorkerCount
    maxParallelForks = (maxWorkerCount < 2) ? 1 : maxWorkerCount / 2
  }
}
