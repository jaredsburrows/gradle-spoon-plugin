plugins {
  id 'org.jetbrains.kotlin.jvm'
  id 'org.jetbrains.dokka'
  id 'org.jlleitschuh.gradle.ktlint'
  id 'com.vanniktech.maven.publish'
  id 'com.gradle.plugin-publish'
  id 'com.github.ben-manes.versions'
  id 'java-gradle-plugin'
  id 'java-library'
  id 'groovy'
}

apply from: "${project.rootDir}/gradle/dependencies.gradle"

task createClasspathManifest() {
  def outputDir = new File(buildDir, name)

  inputs.files sourceSets.main.runtimeClasspath
  outputs.dir outputDir

  doLast {
    outputDir.mkdirs()
    // Combine both main and test plugin classpaths
    def set = new HashSet<String>()
    sourceSets.main.runtimeClasspath.files.forEach {
      set.add(it.path)
    }
    sourceSets.test.runtimeClasspath.files.forEach {
      set.add(it.path)
    }
    def list = new ArrayList<String>(set)
    file("$outputDir/plugin-classpath.txt").text = String.join("\n", list)
  }
}

dependencies {
  compileOnly gradleApi()

  implementation deps.kotlin.stdlib.jdk
  implementation deps.spoon.runner
  implementation 'com.xenomachina:kotlin-argparser:2.0.7' // https://mvnrepository.com/artifact/com.xenomachina/kotlin-argparser/2.0.7
  implementation deps.android.tools.build.gradle
  implementation deps.android.tools.ddmlib

  testRuntimeOnly files(createClasspathManifest)

  testImplementation localGroovy()
  testImplementation gradleTestKit()
  testImplementation deps.spock, { exclude module: 'groovy-all' } // Use localGroovy()
  testImplementation deps.junit
}

gradlePlugin {
  website = POM_URL
  vcsUrl = POM_SCM_URL
  plugins {
    spoonPlugin {
      id = PLUGIN_NAME
      implementationClass = PLUGIN_NAME_CLASS
      displayName = POM_NAME
      description = POM_DESCRIPTION
      tags.set(['spoon'])
    }
  }
}
